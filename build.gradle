/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
    }

	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
	}
}

apply plugin: 'groovy'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'

description = 'Gradle plugin to build FirstSpirit modules (FSMs).'
group = 'com.github.moritzzimmer'
version = '0.3.0'
ext.pluginName = 'gradle-fsm-plugin'

repositories {
	mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
	
    testCompile (
		'junit:junit:4.11',
		'org.hamcrest:hamcrest-all:1.3'
	)
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Gradle FSM plugin',
				   'Implementation-Version': version,
				   'Built-By': System.getProperty('user.name'),
				   'Built-Date': new Date(),
				   'Built-JDK': System.getProperty('java.version'),
				   'Built-Gradle': gradle.gradleVersion
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId pluginName
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle FSM plugin')
                root.appendNode('description', 'Gradle plugin to build FirstSpirit modules (FSMs).')
                root.appendNode('inceptionYear', '2013')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def developer = developers.appendNode('developer')
                developer.appendNode('id', 'moritzzimmer')
                developer.appendNode('name', 'Moritz Zimmer')
                developer.appendNode('email', 'moritz.zmmr@gmail.com')
            }
        }
    }
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
	key =  project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null

    publications = ['mavenJava']

	pkg {
		repo = 'gradle-plugins'
		name = pluginName
		desc = 'Gradle plugin to build FirstSpirit modules (FSMs).'
		licenses = ['Apache-2.0']
        version {
            attributes = ['gradle-plugin': 'com.github.moritzzimmer.fsm:com.github.moritzzimmer:gradle-fsm-plugin']
        }
	}
}

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal'
        , group:'publishing', description: 'Installs the maven Java artifact to the local Maven repository') << {
    logger.info "Installed $project.name"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
