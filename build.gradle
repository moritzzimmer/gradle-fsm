/*
 * Copyright 2013 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
	repositories {
		maven { url 'http://jcenter.bintray.com' }
	}

	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
	}
}

apply plugin: 'groovy'
apply plugin: 'maven' // use maven-publish as soon as the plugin supports signing
apply plugin: 'bintray'
apply plugin: 'signing'
apply plugin: 'eclipse'

description = 'Gradle plugin to build FirstSpirit modules (FSMs).'
group = 'org.gradle.api.plugins'
version = '0.2.0'

repositories {
	mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
	
    testCompile (
		'junit:junit:4.11',
		'org.hamcrest:hamcrest-all:1.3'
	)
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Gradle FSM plugin',
				   'Implementation-Version': version,
				   'Built-By': System.getProperty('user.name'),
				   'Built-Date': new Date(),
				   'Built-JDK': System.getProperty('java.version'),
				   'Built-Gradle': gradle.gradleVersion
	}
}

task sourcesJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
	classifier 'groovydoc'
	from groovydoc.destinationDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives groovydocJar
}

signing {
	sign configurations.archives
}

if(hasProperty('nexusUsername') && hasProperty('nexusPassword')) {
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { deployment -> signing.signPom(deployment) }
				
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: nexusUsername, password: nexusPassword)
				}
				
				pom.project {
					name 'Gradle FSM plugin'
					artifactId = 'gradle-fsm-plugin'
					description 'Gradle plugin to build FirstSpirit modules (FSMs).'
					inceptionYear '2013'
					
					scm {
						url 'https://github.com/moritzzimmer/gradle-fsm'
						connection 'scm:git://github.com/moritzzimmer/gradle-fsm.git'
						developerConnection 'scm:git@github.com:moritzzimmer/gradle-fsm.git'
					}
					
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					
					developers {
						developer {
							id 'moritzzimmer'
							name 'Moritz Zimmer'
							email 'moritz.zmmr@gmail.com'
						}
					}
				}
			}
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
	key =  project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null

	configurations = ['archives']

	pkg {
		repo = 'gradle-plugins'
		name = 'gradle-fsm-plugin'
		desc = 'Gradle plugin to build FirstSpirit modules (FSMs).'
		licenses = ['Apache-2.0']
	}
}
